use.miden::kernels::tx::memory

#! Returns the block number of the last known block at the time of transaction execution.
#!
#! Inputs: []
#! Outputs: [num]
#!
#! num is the last known block number.
export.get_block_number
    push.0
    # => [0]

    exec.memory::get_block_number_ptr
    syscall.exec_kernel_proc
    # => [num]
end

#! Returns the block hash of the reference block.
#!
#! Stack: [EMPTY_WORD]
#! Output: [BLOCK_HASH]
#!
#! Where:
#! - BLOCK_HASH, reference block for the transaction execution.
export.get_block_hash
    padw 
    exec.memory::get_block_hash_ptr
    syscall.exec_kernel_proc
    # => [BLOCK_HASH]
end

#! Returns the input notes commitment hash.
#!
#! See `transaction::api::get_input_notes_commitment` for details.
#!
#! Stack: []
#! Outputs: [INPUT_NOTES_COMMITMENT]
#!
#! Where:
#! - INPUT_NOTES_COMMITMENT is the input notes commitment hash.
export.get_input_notes_commitment
    padw 
    exec.memory::get_input_notes_commitment_ptr
    syscall.exec_kernel_proc
    # => [INPUT_NOTES_COMMITMENT]
end

#! Returns the output notes hash. This is computed as a sequential hash of (note_id, note_metadata)
#! tuples over all output notes.
#!
#! Inputs: [0, 0, 0, 0]
#! Outputs: [COM]
#!
#! COM is the output notes hash.
export.get_output_notes_hash
    padw
    # => [0, 0, 0, 0]

    exec.memory::get_output_notes_hash_ptr
    syscall.exec_kernel_proc
    # => [COM]
end

#! Creates a new note and returns the index of the note.
#!
#! Inputs: [tag, aux, note_type, RECIPIENT]
#! Outputs: [note_idx]
#!
#! tag is the tag to be included in the note.
#! aux is the auxiliary metadata to be included in the note.
#! note_type is the storage type of the note
#! RECIPIENT is the recipient of the note.
#! note_idx is the index of the crated note.
export.create_note
    exec.memory::create_note_ptr
    syscall.exec_kernel_proc
    # => [note_idx, EMPTY_WORD, 0]

    # clear the padding from the kernel response
    movdn.4 dropw swap drop
    # => [note_idx]
end

#! Adds the ASSET to the note specified by the index.
#!
#! Inputs: [note_idx, ASSET]
#! Outputs: [note_idx]
#!
#! note_idx is the index of the note to which the asset is added.
#! ASSET can be a fungible or non-fungible asset.
export.add_asset_to_note
    exec.memory::add_asset_to_note_ptr
    syscall.exec_kernel_proc
    # => [note_idx, EMPTY_WORD]

    # clear the padding from the kernel response
    movdn.4 dropw
    # => [note_idx]
end

#! Returns the RECIPIENT for a specified SERIAL_NUM, SCRIPT_HASH, and inputs hash
#!
#! Inputs: [SERIAL_NUM, SCRIPT_HASH, INPUT_HASH]
#! Outputs: [RECIPIENT]
#!
export.build_recipient_hash
  padw hmerge
  # => [SERIAL_NUM_HASH, SCRIPT_HASH, INPUT_HASH]

  swapw hmerge
  # => [MERGE_SCRIPT, INPUT_HASH]

  swapw hmerge
  # [RECIPIENT]
end
